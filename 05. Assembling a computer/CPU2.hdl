/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPUresident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU2 {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to restart the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    Mux16(a=instruction, b=ALUout, sel=instruction[15], out=RegAin); // выбор загрузки из ALU или из instruction
    Mux(a=true, b=instruction[5], sel=instruction[15], out=ALUA); // загрука RegA
    ARegister(in=RegAin, load=ALUA, out=RegA, out[0..14]=addressM); // регистр A выход addressM

    And(a=instruction[15], b=instruction[4], out=loadD0); // условия загруки RegD если инструкция C
    And(a=loadD0, b=instruction[14], out=loadD);
    Not(in=instruction[14], out=N14);
    And(a=loadD0, b=N14, out=loadD1);

    DRegister(in=ALUout, load=loadD, out=RegD0);
    Register(in=ALUout, load=loadD1, out=RegD1);

    Mux16(a=RegD1, b=RegD0, sel=instruction[13], out=RegD);

    Mux16(a=RegA, b=inM, sel=instruction[12], out=ALUin); // выбрать A или M
    ALU(x=RegD, y=ALUin, zx=instruction[11], nx=instruction[10],
     zy=instruction[9], ny=instruction[8], f=instruction[7],
     no=instruction[6], out=ALUout, out=outM, zr=ALUzr, ng=ALUng);

    And(a=instruction[15], b=instruction[3], out=writeM); // загружаем в RAM если инструкция C 


    // вычислить PCload и PCinc
    And(a=ALUzr, b=instruction[1], out=JEQ); // перейти, если ноль
    And(a=ALUng, b=instruction[2], out=JLT); // перейти, если отрицательное
    Or(a=ALUzr, b=ALUng, out=ZERONEG);
    Not(in=ZERONEG, out=POS); // положительное (не ноль и не отрицательное)
    And(a=POS, b=instruction[0], out=JGT); // перейти, если положительное
    Or(a=JEQ, b=JLT, out=JLE);
    Or(a=JLE, b=JGT, out=JMP);              // переход
    And(a=instruction[15], b=JMP, out=PCload); // переход если инструкция C
    PC(in=RegA, inc=true, load=PCload, reset=reset, out[0..14]=pc);
}
