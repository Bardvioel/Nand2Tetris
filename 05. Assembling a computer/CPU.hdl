/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    
    Mux16(a=instruction, b=ALU-out, sel=instruction[15], out=Reg-A-in); // выбор загрузки из ALU или из instruction
    Mux(a=true, b=instruction[5], sel=instruction[15], out=ALU-A); // загрука Reg-A
    ARegister(in=Reg-A-in, load=ALU-A, out=Reg-A, out[0..14]=addressM); // регистр A выход addressM

    And(a=instruction[15], b=instruction[4], out=load-D); // условия загруки Reg-D если инструкция C
    DRegister(in=ALU-out, load=load-D, out=Reg-D);

    Mux16(a=Reg-A, b=inM, sel=instruction[12], out=ALU-in); // выбрать A или M
    ALU(x=Reg-D, y=ALU-in, zx=instruction[11], nx=instruction[10],
     zy=instruction[9], ny=instruction[8], f=instruction[7],
     no=instruction[6], out=ALU-out, out=outM, zr=ALU-zr, ng=ALU-ng);

    And(a=instruction[15], b=instruction[3], out=writeM); // загружаем в RAM если инструкция C 


    // вычислить PC-load и PC-inc
    And(a=ALU-zr, b=instruction[1], out=JEQ); // перейти, если ноль
    And(a=ALU-ng, b=instruction[2], out=JLT); // перейти, если отрицательное
    Or(a=ALU-zr, b=ALU-ng, out=ZERO-NEG);
    Not(in=ZERO-NEG, out=POS); // положительное (не ноль и не отрицательное)
    And(a=POS, b=instruction[0], out=JGT); // перейти, если положительное
    Or(a=JEQ, b=JLT, out=JLE);
    Or(a=JLE, b=JGT, out=JMP);              // переход
    And(a=instruction[15], b=JMP, out=PC-load); // переход если инструкция C
    PC(in=Reg-A, inc=true, load=PC-load, reset=reset, out[0..14]=pc);
}
